#!/usr/bin/env just --justfile
container_repository_username := 'sandorex'
container_repository := 'ghcr.io'

images_dir := justfile_directory()
dotfiles_dir := canonicalize(join(justfile_directory(), '../dotfiles'))
repository_prefix := container_repository + '/' + container_repository_username
git_sha := shell('git rev-parse --short=10 HEAD')

images := "fedora-base fedora"

_default:
    @just --justfile {{ justfile() }} --list

# list images
images:
    @echo "{{ images }}"

# build all images in order
build-all: (build images)

# build image
build +IMAGES:
    #!/usr/bin/env bash
    set -euo pipefail

    for image in {{ IMAGES }}; do
        echo "Building image ${image:?}"
        
        # im tagging it multiple times intentionally
        # arcam-<image>:latest
        # arcam-<image>:<git_sha>
        # <repository>/<owner>/arcam-<image>:latest
        # <repository>/<owner>/arcam-<image>:<git_sha>
        #
        # --build-arg=SHA is important so the correct image is used as the base but
        # this means the images need to be built in order, at least base images
        podman build \
            --security-opt label=disable \
            --cache-ttl=10h \
            --build-arg="SHA={{ git_sha }}" \
            -t "arcam-$image:latest" \
            -t "arcam-$image:{{ git_sha }}" \
            -t "{{ repository_prefix }}/arcam-$image:latest" \
            -t "{{ repository_prefix }}/arcam-$image:{{ git_sha }}" \
            -f "{{ images_dir }}/Containerfile.$image" \
            --volume "{{ dotfiles_dir }}:/dotfiles:ro" \
            "{{ justfile_directory() }}"

        echo
    done

# build and publish all images
publish-all: (publish images)

# build an publish image to the repository (requires podman to be logged in)
publish +IMAGES: (build IMAGES)
    #!/usr/bin/env bash
    set -euo pipefail

    for image in {{ IMAGES }}; do
        echo "Publishing image ${image:?}"

        # push git sha tagged
        podman image push "$image" "{{ repository_prefix }}/arcam-$image:{{ git_sha }}"

        # push as latest
        podman image push "$image" "{{ repository_prefix }}/arcam-$image:latest"
        
        echo
    done
